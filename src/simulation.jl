export simulate, SimulationResult, visualize

using Plots: Plot, plot
using StaticArrays
using Unrolled

"Results are indexed by (time, agent, component)"
@kwdef struct SimulationResult
    times::Vector{ùïã}
    values::Array{‚Ñù,3}
    components::Vector{String}
end

SimulationResult(times::Vector{ùïã}, num_agents::‚Ñï, components::Vector{String}) = begin
    values = Array{‚Ñù}(undef, length(times), num_agents, length(components))
    SimulationResult(times, values, components) 
end 

"A generic result visualization function that draws multiple line plots, 
one for each component of the state vector.\n"
function visualize(
    result::SimulationResult; delta_t=nothing
)::Vector{Plot}
    if delta_t === nothing
        times = result.times
        xlabel = "time step"
    else
        times = result.times .* delta_t 
        xlabel = "time (s)"
    end
    ## currently assume all agents have the same type of states and observations
    labels = reshape(["agent $i" for i in 1:size(result.values, 2)], (1, :))
    map(enumerate(result.components)) do (c_id, comp)
        ys = result.values[:,:,c_id]
        plot(times, ys; title=comp, label=labels, xlabel)
    end
end

        
@kwdef struct AgentState{X,Z,U,Msg}
    state_queue::FixedQueue{X}
    obs_queue::FixedQueue{Z}
    act_queue::FixedQueue{U}
    msg_queue::MsgQueue{Msg}
    controller::Controller{X,Z,U,Msg}
end
  

"""
Simulate multiple distributed agents with the given initial states and controllers and 
return the resulting trajectory as a `Simulation` struct.

# Arguments
- `times::Vector{ùïã}`: the time instances at which the simulation should save the states in 
the result. The first element in this sequence should be the time of the given initial states. 
- `recorder`: a tuple of the shape `(components::Vector{String}, record_f)`, where 
   `record_f` should takes in `(xs, zs, us)` and returns a real matrix of the size 
   `(num agents, length(components))`.
"""
function simulate(
    world_dynamics::WorldDynamics{N},
    delay_model::DelayModel,
    framework::ControllerFramework{X,Z,U,Msg},
    init_status::Each{Tuple{X,Z,U}},
    recorder::Tuple{Vector{String},Function},
    times::Vector{ùïã},
)::SimulationResult where {X,Z,U,Msg,N}
    (controllers, msg_qs) = make_controllers(framework, init_status, times[1])
    
    make_agent(id::‚Ñï)::AgentState = begin
        (x‚ÇÄ, z‚ÇÄ, u‚ÇÄ) = init_status[id]
        AgentState(
            state_queue=constant_queue(x‚ÇÄ, delay_model.obs),
            obs_queue=constant_queue(z‚ÇÄ, delay_model.obs),
            act_queue=constant_queue(u‚ÇÄ, delay_model.act),
            msg_queue=msg_qs[id],
            controller=controllers[id],
        )
    end
    simulate_impl(
        world_dynamics,
        map(make_agent, SVector{N}(1:N)),
        SVector{N}(init_status),
        recorder,
        times,
    )
end

@unroll function simulate_impl(
    world_dynamics::WorldDynamics{N},
    agents::SVector{N,AgentState{X,Z,U,Msg}},
    init_status::SVector{N,Tuple{X,Z,U}},
    recorder::Tuple{Vector{String},RF},
    times,
) where {X,Z,U,Msg,RF,N}
    @assert !isempty(times)
    Each = MVector{N}

    xs, zs, us = @unzip(MVector(init_status), Each{Tuple{X,Z,U}})
    result = SimulationResult(times, N, recorder[1])
    data_idx = 1
    for t in times[1]:times[end]
        # observe, control, and send messages
        transmition = MMatrix{N,N,Msg}(undef)  # indexed by (receiver, sender)
        @unroll for i in 1:length(agents)  # unroll away dynamic dispatch at compile time!
            a = agents[i]
            x = pushpop!(a.state_queue, xs[i])
            z = pushpop!(a.obs_queue, zs[i])
            ms = first(a.msg_queue)
            u, ms‚Ä≤ = control!(a.controller, x, z, ms)
            us[i] = pushpop!(a.act_queue, u)
            transmition[:,i] = ms‚Ä≤
        end
        # receive messagees and update world states
        @unroll for i in 1:length(agents)
            a = agents[i]
            pushpop!(a.msg_queue, convert(Vector{Msg}, transmition[i,:]))
            xs[i] = sys_forward(world_dynamics.dynamics[i], xs[i], us[i], t)
            zs[i] = obs_forward(world_dynamics.obs_dynamics[i], xs[i], zs[i], t)
        end
        # record results
        if t == times[data_idx]
            data = recorder[2](xs, zs, us)
            @assert size(data) == (N, length(recorder[1])) ("The recorder should "
               * "return a matrix of size (N * Num curves), got size $(size(data))")
            result.values[data_idx, :, :] = data
            data_idx += 1
        end
    end
    result
end